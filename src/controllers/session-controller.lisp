(in-package #:rest-server.db)

(defmethod control-store ((type (eql :session)) req res)
  (let* ((payload (util:get-payload req))
         (mail (util:agetf :mail payload))
         (pass (util:agetf :password payload)))
    (if (or (null mail) (null pass))
        (util:http-response (400)
          "Malformed login data")
        (let* ((dao (mito:find-dao 'db:user :mail mail))
               (alist (if dao (util:dao->alist dao) nil)))
          (cond ((null dao)
                 (util:http-response (404)
                   "Unknown user"))
                ((mito-auth:auth dao pass)
                 (util:http-response ()
                   `((:id . ,(util:agetf :id alist))
                     (:mail . ,mail)
                     (:token . "")))) ; TODO: JWT token
                (t (util:http-response (403)
                     "Wrong password")))))))
